# El alambique viajero

- Enunciado [acÃ¡](https://docs.google.com/document/u/1/d/e/2PACX-1vTpecrFHsM1-sH4kyp5Rt8RljpZig-XgHCjVh9oTZDcL6zBpTmG4OTb9SqPgIQvLQL6QSX0LfPvV5Gb/pub)
- SoluciÃ³n propuesta [acÃ¡](https://github.com/pdepjm/2023-o-alambique-veloz)

### Â¿CÃ³mo resolverlo? 
Â¡Hola! Esta es una guÃ­a del TP de objetos, con el objetivo de guiar a la soluciÃ³n propuesta y ver errores comunes que pueden aparecer en el camino. Pueden existir soluciones diferentes, este documento plantea una posible.

Luego de leer todo el enunciado, podemos comenzar por la primera parte, que nos pide concretamente quÃ© tiene que poder cumplir nuestro sistema:

> **Averiguar**
> 1. CuÃ¡ntos lugares visitÃ³ Luke
> 2. El recuerdo que se trajo del Ãºltimo lugar que visitÃ³

Con esto, nos da una idea de lo que posiblemente queremos poder consultar. En otras palabras, sabemos que mensajes son los _principales_ y quiÃ©n deberÃ­a contestarlos: `Luke`. Queremos poder hacer:

```wollok
luke.cantidadLugaresVisitados()
>>> 5
luke.ultimoRecuerdo()
>>> "llavero"
```

Esto cumple con los dos puntos que nos pide el enunciado, Â¡ahora hagamos que funcione! Se puede ver a simple vista con lo que pensamos hasta ahora que necesitamos:

- Un objeto llamado luke
- Que ese objeto entienda dos mensajes: `cantidadLugaresVisitados` y `ultimoRecuerdo`

Planteamos entonces esta estructura, luego la vamos completando de a poco:

```wollok
object luke{
  method cantidadLugaresVisitados()

  method ultimoRecuerdo()
}
```

Â¿QuÃ© tiene que hacer `cantidadLugaresVisitados`? Â¿Es de **consulta** o de **efecto**? Â¡De **consulta**! Queremos que este mÃ©todo nos **devuelva** un nÃºmero, concretamente la cantidad de lugares a los que viajo.
Sabemos gracias al resto del enunciado que Luke va a poder hacer un viaje (veremos mÃ¡s adelante), asi que tiene sentido que esa cantidad de lugares vaya cambiando con el paso del tiempo, ya que con cada viaje que haga, vamos a tener que sumarle 1 a este valor.
Entonces agreguemosle un atributo a Luke, y hagamos que este mÃ©todo sea un _getter_, es decir nos devuelva el valor de dicho atributo. Luego veremos cÃ³mo incrementarlo.

```wollok
object luke{
  var cantidadLugaresVisitados = 0
  method cantidadLugaresVisitados() = cantidadLugaresVisitados

  method ultimoRecuerdo()
}
```

#### âš  Error comÃºn 

Ja, tengo un atributo y necesito el **getter**. Â¡uso `property` y me ahorro escribir el mÃ©todo!:

```wollok
object luke{
  var property cantidadLugaresVisitados = 0

  method ultimoRecuerdo()
}
```

Este es un error muy comÃºn y que representa un **error conceptual** que nos interesa evaluar. Repasemos quÃ© hacÃ­a esta herramienta.

El `property` lo que hace es darnos tanto el **getter** como el **setter**, es decir que permite que **externamente** se pueda leer y modificar el atributo (es decir parte del **estado intero**) de mi objeto.

Â¿QuÃ© pasa entonces? El estado interno de un objeto, es justamente **interno**, vamos a querer que la menor cantidad de objetos externos puedan tener acceso a modificarlos o leerlos. A este concepto le llamamos **encapsulamiento**, 
poner setters y getters cuando no son estrictamente necesarios implica **romper el encapsulamiento**, y justamente poner el **property** sin un anÃ¡lisis de si necesito **ambos** (setter y getter) implica que no
sabemos analizar el encapsulamiento de un objeto.

Â¿Entonces no usamos el `property`? Si lo podemos usar, pero _analizando_ cuando lo queremos y cuando no:

- Â¿Necesito tener solo el getter? Hago un mÃ©todo getter a mano - **no uso property**
- Â¿Necesito tener solo el setter? Hago un mÃ©todo setter a mano - **no uso property**
- Â¿No necesito ninguno de los dos? **No uso property**
- Â¿Necesito tanto el getter como el setter? Puedo hacer el mÃ©todo getter y el mÃ©todo setter a mano, pero tambiÃ©n puedo ahorrarme eso y usar property

En este caso, por el momento solo nos interesa poder **consultar desde afuera** la cantidad de lugares que visitÃ³, todavÃ­a no sabemos si necesitamos poder cambiarlo desde afuera (y spoiler, no vamos a querer esto), asi que nos quedamos con esta soluciÃ³n:

```wollok
object luke{
  var cantidadLugaresVisitados = 0
  method cantidadLugaresVisitados() = cantidadLugaresVisitados

  method ultimoRecuerdo()
}
```


Sigamos con el mÃ©todo `ultimoRecuerdo`, para esto necesitamos recordar quÃ© nos dice el enunciado sobre los recuerdos:
> Cuando viaja se trae un recuerdo tÃ­pico del lugar visitado que conserva en un lugar destacado de su casa. El problema es que su casa es pequeÃ±a, por lo que tira el recuerdo que haya traÃ­do de algÃºn viaje anterior. 

Sobre los recuerdos nos dicen que cambia cuando viaja, pero no se guarda todos si no que guarda uno solo -> Â¡podemos guardarlo en un atributo! 

```wollok
object luke{
  var cantidadLugaresVisitados = 0
  var ultimoRecuerdo = ""

  method cantidadLugaresVisitados() = cantidadLugaresVisitados

  method ultimoRecuerdo() = ultimoRecuerdo
}
```

Ya tenemos los mÃ©todos que necesitabamos para lo que nos pedÃ­a el enunciado. Pero nos hablan tambiÃ©n de la idea de que Luke pueda **viajar**, veamos quÃ© otra informaciÃ³n nos dan sobre esto:
> El vehÃ­culo utilizado para viajar sufre las consecuencias. Cuando pretende visitar una ciudad a la que no puede ir, simplemente no va.

Por lo que leÃ­mos, viajar implica las siguientes cosas:
- El vehiculo con el que viaja sufre consecuencias.
- Sumamos en 1 a la _cantidad de lugares visitados_, ya que justamente visitamos un lugar al viajar
- Nos cambia el Ãºltimo recuerdo, siendo este el recuerdo tÃ­pico de la ciudad a la que viajÃ³

Y ademÃ¡s de esto tenemos que tener en cuenta quÃ©
- Hay ciudades a las cuales no puede ir, por lo que no siempre va a viajar

Son un montÃ³n de cosas ğŸ¤¯, pensemos primero cÃ³mo lo usarÃ­amos en la consola, Â¿quiÃ©n va a viajar? Â¡Luke! Entonces tiene sentido que en la consola hagamos algo como:

```wollok
>>> luke.viajar()
```

Pero nos hablan de ciudades, es decir que no siempre viaja al mismo lugar -> es algo que va a ser diferente cada vez que enviemos el mensaje Â¡usemos un parÃ¡metro entonces!

```wollok
>>> luke.viajarA(ciudad)
```

Ahora podemos ver mÃ¡s claro que necesitamos que Luke entienda el mensaje `viajarA`, dandole una ciudad a la cual viajar -> Â¡tenemos que definir un mÃ©todo! Â¿De efecto o de consulta? Si analizamos la lista que vimos antes sobre las cosas que debÃ­an ocurrir cuando se viajaba, apuntan a que son cosas que cambian estado, por lo que tiene sentido por ahora que sea un mÃ©todo de efecto.

```wollok
object luke{
  var cantidadLugaresVisitados = 0
  var ultimoRecuerdo = ""

  method cantidadLugaresVisitados() = cantidadLugaresVisitados

  method ultimoRecuerdo() = ultimoRecuerdo

  method viajarA(ciudad){
    //solo viajar si puede
    //el vehiculo tiene consecuencias
    //sumar cantidad de lugares visitados
    //cambiar ultimo recuerdo
  }
}
```

#### âš  Error comÃºn 

Algunas personas pueden haberlo modelado a `luke` y a la `casa` como dos objetos diferentes, y que uno de los dos cambie el estado del otro (por ejemplo que Luke cambie atributos de la Casa) al viajar. PodrÃ­a ser una soluciÃ³n vÃ¡lida si la csas tuviera algun valor agregado a solo guardar la cantidad de visitar y el recuerdo, dado que no tiene mÃ¡s complejidad que esa, podemos simplemente dejar esto como atributos de Luke y simplificar las cosas.

#### Volviendo al ejercicio
Sumar la cantidad es algo que sale rÃ¡pido, ya que tenemos un atributo que guarda esto, simplemente podemos aumentarlo en 1: 

```wollok
method viajarA(ciudad){
  //solo viajar si puede
  //el vehiculo tiene consecuencias
  cantidadLugaresVisitados += 1
  //cambiar ultimo recuerdo
}
```

Cambiar el Ãºltimo recuerdo tambiÃ©n suena como algo sencillo, es solo hacer `ultimo recuerdo = algo` Â¿no? Â¿Pero ese `algo` que es? Viendo cÃ³mo sigue el enunciado:

>Se conocen los siguientes recuerdos
> - El recuerdo tÃ­pico de ParÃ­s es un llavero de la torre eiffel
> - Buenos Aires tiene como recuerdo tÃ­pico un mate, pero dependiendo de quiÃ©n sea el presidente puede venir con yerba o no.
> - etc

Vemos que cada una de las ciudades tiene un recuerdo diferente, entonces una opciÃ³n para solucionar esto es Â¡preguntarle a la ciudad!

```wollok
method viajarA(ciudad){
  //solo viajar si puede
  //el vehiculo tiene consecuencias
  cantidadLugaresVisitados += 1
  ultimoRecuerdo = ciudad.recuerdo()
}
```

De esta forma, nos despreocupamos si a futuro tenemos muchas mÃ¡s ciudades posibles, siempre y cuando tengan un recuerdo podemos agregar muuchas mÃ¡s y nuestro cÃ³digo sigue funcionando. 

Estamos planteando con esto: `ciudad.recuerdo()` que las ciudades serÃ¡n **objetos** que compartan una interfaz: el mensaje `recuerdo`. Las veremos mÃ¡s adelante pero nos sirve ya comenzar a pensar en ellas como objetos.

#### âš  Error comÃºn 

Otra forma de modelar esta parte es usando _strings_ para representar las ciudades y hacer varios `ifs`:

```wollok
method viajarA(ciudad){
  ...//resto de cÃ³digo
  if(ciudad == "buenos aires"){
    ultimoRecuerdo = "mate"
  }else if(ciudad == "paris"){
    ultimoRecuerdo = "llavero"
  }else if{
    ...//etc
  }
}
```

El problema de esto es que estamos limitando bastante a las ciudades, habiendo leÃ­do el enunciado podemos ver que hay mÃ¡s cosas que dependen de las ciudades, por lo que significa que tenerlas como strings nos ata a plagar nuestro cÃ³digo de `ifs` anidados cada vez que tengamos algo que dependa de ellas.
AdemÃ¡s de esto, caemos en que no es tan fÃ¡cil agregar nuevas ciudades, agregar una implicarÃ­a cambiar todos estos ifs.

En lugar de esto, la opciÃ³n que presentamos **usando polimorfismo** nos permite que mientras las nuevas ciudades entiendan el mensaje polimorfico `recuerdo`, Â¡no cambiemos nada!

#### Volviendo al ejercicio

Siguiendo con el mÃ©todo `viajarA` que dejamos a la mitad, nos queda completar que solo viaje si puede, Â¿quÃ© significa esto? Leamos un poco mÃ¡s el enunciado:

> Para poder ir a las ciudades, hay diferentes restricciones dependiendo del vehÃ­culo en que se pretenda ir.
> - ParÃ­s, el alambique veloz tiene que tener el tanque de combustible lleno
> - Buenos Aires, el vehÃ­culo tiene que ser rÃ¡pido
> - Bagdad no hay restricciones
> - Las Vegas: la misma restricciÃ³n del lugar que se estÃ© homenajeando

Estas restricciones, implican que si no cumplen con ellas, no deberÃ­amos _viajar_, es decir que no deberÃ­amos ni cambiar el recuerdo, ni sumar un lugar visitado, ni hacer que el vehiculo tenga consecuencias (que veremos mÃ¡s adelante). Suena a que necesitamos que esto solo se cumpla, si se cumple cierta condiciÃ³n Â¡necesitamos una alternativa condicional!

```wollok
method viajarA(ciudad){
  if(/*una condicion*/){
    //el vehiculo tiene consecuencias
    cantidadLugaresVisitados += 1
    ultimoRecuerdo = ciudad.recuerdo()
  }
}
```

Hacemos que estas tres cosas sucedan unicamente cuando la condiciÃ³n se cumple. Â¿CuÃ¡l es la condiciÃ³n? Por lo que leÃ­mos reciÃ©n, depende **de cada ciudad**, Â¡y las ciudades son objetos! Es decir que podemos **delegar** esto a ellas, de la misma forma que hicimos con el recuerdo:

```wollok
method viajarA(ciudad){
  if(ciudad.puedeViajar()){
    //el vehiculo tiene consecuencias
    cantidadLugaresVisitados += 1
    ultimoRecuerdo = ciudad.recuerdo()
  }
}
```

#### âš  Error comÃºn 

Utilizar ifs anidados para saber por cada ciudad si puede o no viajar, cae en el mismo problema mencionado antes con el recuerdo, nos conviene usar una soluciÃ³n con **polimorfismo**, que a luke no le interese cÃ³mo implementa cada ciudad la restricciÃ³n, si no que justamente sea responsabilidad de las ciudades.

#### Volviendo al ejercicio

Â¿Pero con eso ya estÃ¡ la condiciÃ³n? ğŸ¤” LeÃ­mos que las restricciones dependen del **vehiculo** que estÃ© usando Luke, por lo que tiene sentido quizÃ¡s que le demos por parÃ¡metro un vehiculo, y que a partir de este pueda considerar si cumple o no con la restricciÃ³n.

El vehÃ­culo, ademÃ¡s, vemos que sufre consecuencias -> Â¿quizÃ¡s tambiÃ©n es un objeto? Podemos tratarlos como tales por el momento y ver mÃ¡s adelante si nos sirve o no. Vamos a agregarle a luke un atributo con el vehiculo, y hacer que el vehiculo viaje cuando luke viaje:

```wollok
object luke{
  var cantidadLugaresVisitados = 0
  var ultimoRecuerdo = ""
  var vehiculo

  method cantidadLugaresVisitados() = cantidadLugaresVisitados

  method ultimoRecuerdo() = ultimoRecuerdo

  method viajarA(ciudad){
    if(ciudad.puedeViajarCon(vehiculo)){
      vehiculo.viajar()
      cantidadLugaresVisitados += 1
      ultimoRecuerdo = ciudad.recuerdo()
    }
  }  
}
```

Ya que estamos, podemos delegar un poco mÃ¡s la lÃ³gica de este mÃ©todo, de forma que nos quede mÃ¡s _declarativo_. Lo que deberÃ­a ocuparse `viajarA` es de _realizar el viaje_ si es que _puede hacerlo_, entonces podemos delegar la lÃ³gica de **realmente realizar el viaje** (hacer que el vehiculo viaje, etc) a otro mÃ©todo:

```wollok
object luke{
  var cantidadLugaresVisitados = 0
  var ultimoRecuerdo = ""
  var vehiculo

  method cantidadLugaresVisitados() = cantidadLugaresVisitados

  method ultimoRecuerdo() = ultimoRecuerdo

  method viajarA(ciudad){
    if(ciudad.puedeViajarCon(vehiculo)){
      self.realmenteViajar() //es un mensaje mio, uso self entonces
    }
  }

  method realmenteViajar(){
      vehiculo.viajar()
      cantidadLugaresVisitados += 1
      ultimoRecuerdo = ciudad.recuerdo()
  }  
}
```

Â¡Tenemos a Luke! Cumple con lo que nos pide el enunciado, pero todavÃ­a nos estÃ¡ faltando codear las ciudades y los vehiculos; pero habiendo hecho primero a Luke, partiendo del problema principal, hicimos un montÃ³n de anÃ¡lisis que ya nos sirve para saber por ejemplo que las ciudades son objetos y que necesitan entender los mensajes `recuerdo` y `puedeViajarCon`, Â¡es un montÃ³n!

#### âš  Error comÃºn 

Un modelado que surgiÃ³ con este TP fue hacer que las **ciudades** o los **vehiculos** se encargaran de cambiar la cantidad de lugares visitados y el ultimo recuerdo de Luke. Esto nos trae dos problemas ligados a la repeticiÃ³n de lÃ³gica:

- Al repetirlo en todas las ciudades (porque siempre sucede, por ende todas las ciudades deberÃ­an repetirlo), si queremos cambiar algo en esa lÃ³gica, como decir que el Ãºltimo recuerdo es siempre "chocolate", hay que recordar que hay que cambiarlo en **toooodas** las ciudades (bastante engorroso).
- Estamos atandonos a recordar que si agregamos nuevas ciudades, hay que agregar que cambien el estado de este otro objeto que a primera vista no vemos quÃ© tiene que ver -> resulta en cÃ³digo confuso de escalar.

Tanto el Ãºltimo recuerdo y la cantidad de lugares visitados son parte del estado propio de Luke, por lo que tiene sentido que la responsabilidad de hacer estos cambios sea de ese objeto. 

Por otro lado, queremos que todos estos cambios se realicen al enviar un solo mensaje, si no nos atamos a que viajar implica enviar varios mensajes a varios objetos, es decir algo como:

```wollok
>>>luke.viajar()
>>>ciudad.viajar()
>>>vehiculo.viajar()
```

Â¿QuÃ© pasa si me olvido de ejecutar uno de esos comandos? Mi sistema ya no es consistente, es preferible hacerlo solo con un mensaje, y justamente el objeto que mÃ¡s sentido tiene que lo haga es Luke.

#### Volviendo al ejercicio
